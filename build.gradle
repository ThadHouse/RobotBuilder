plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'maven-publish'
    id "com.jfrog.artifactory" version "5.2.5"
    id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '2024.0.0'
    id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.2'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
    id 'com.gradleup.shadow' version '8.3.9'
    id "de.undercouch.download" version "5.6.0"
    id 'org.aim42.htmlSanityCheck' version '2.0.0-rc4'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

wpilibVersioning.buildServerMode = project.hasProperty('buildServer')
wpilibVersioning.releaseMode = project.hasProperty('releaseMode')

repositories {
    mavenCentral()
}
if (project.hasProperty('releaseMode')) {
    wpilibRepositories.addAllReleaseRepositories(project)
} else {
    wpilibRepositories.addAllDevelopmentRepositories(project)
}

if (System.getenv()['RUN_AZURE_ARTIFACTORY_RELEASE'] != null) {
  artifactory {
    contextUrl = 'https://frcmaven.wpi.edu/artifactory' // base artifactory url
    publish {
      repository {
        if (project.hasProperty('releaseMode')) {
          repoKey = 'release'
        } else {
          repoKey = 'development'
        }
        username = System.getenv()['ARTIFACTORY_PUBLISH_USERNAME']
        password = System.getenv()['ARTIFACTORY_PUBLISH_PASSWORD']
      }
      defaults {
          publications ('maven')
      }
    }
    clientConfig.info.setBuildName('RobotBuilder')
  }
  publish.dependsOn artifactoryPublish
}

application {
    mainClass.set("robotbuilder.RobotBuilder")
}

wpilibVersioning.version.finalizeValue()

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

def downloadNewCommands = tasks.register('downloadNewCommands', Download) {
  src 'https://raw.githubusercontent.com/wpilibsuite/allwpilib/main/wpilibNewCommands/WPILibNewCommands.json'
  dest 'build/resources/main/export/vendordeps/WPILibNewCommands.json'
  overwrite true
}

check.dependsOn jacocoTestReport
check.dependsOn htmlSanityCheck

lombok {
    version = "1.18.34"
    sha256 = ""
}

compileJava {
    options.encoding = "UTF-8"
    dependsOn downloadNewCommands
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.velocity:velocity-engine-core:2.4'
    implementation 'org.yaml:snakeyaml:2.3'
    implementation 'commons-io:commons-io:2.17.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:slf4j-jdk14:2.0.16'
    implementation 'com.sun.activation:javax.activation:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    compileOnly 'org.projectlombok:lombok:1.18.34'
}

test {
    testLogging {
        events "failed"
        exceptionFormat = "full"
    }
}

jar {
    manifest {
        attributes('Implementation-Title': 'RobotBuilder',
                    'Implementation-Version': wpilibVersioning.version.get(),
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': new Date(),
                    'Built-JDK': System.getProperty('java.version'))
    }
}

import org.aim42.htmlsanitycheck.check.*

htmlSanityCheck {
    sourceDir = new File( "src/main/resources/help" )

    // where to put results of sanityChecks...
    checkingResultsDir = new File( "$buildDir/reports/htmlchecks" )

    // fail build on errors?
    failOnErrors = true

    checkerClasses = [BrokenCrossReferencesChecker,
             // BrokenHttpLinksChecker, //Disable link checker due to hanging on Vex links
             DuplicateIdChecker,
             ImageMapChecker,
             MissingAltInImageTagsChecker,
             MissingLocalResourcesChecker]

}

publishing {
    publications {
        maven(MavenPublication) {
            artifact(shadowJar) {
                classifier = null
            }
            groupId = 'edu.wpi.first.tools'
            artifactId = 'RobotBuilder'
            version = wpilibVersioning.version.get()
        }
    }
}

